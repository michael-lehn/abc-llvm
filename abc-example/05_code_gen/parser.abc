#include <stdio.hdr>

#include "error.hdr"
#include "lexer.hdr"
#include "parser.hdr"
#include "symtab.hdr"

fn parseAssignmentExpression() : -> Expr;
fn parseAdditiveExpression() : -> Expr;
fn parseMultiplicativeExpression() : -> Expr;
fn parsePrimaryExpression() : -> Expr;

fn parseExpression() : -> Expr
{
    return parseAssignmentExpression();
}

fn parseAssignmentExpression() : -> Expr
{
    local expr: -> Expr = parseAdditiveExpression();

    while (token.kind == EQUAL) {
	if (!isIdentifierExpr(expr)) {
	    printf("expression is not assignable\n");
            return nullptr;
	}
        getToken();

	local expr2: -> Expr = parseAssignmentExpression();
	if (!expr2) {
	    errorMsg(token.loc, "expression expected\n");
	    errorFatal();
            return nullptr;
        }
	expr = createBinaryExpr(EXPR_ASSIGN, expr, expr2);
    }
    return expr;
}

fn parseAdditiveExpression() : -> Expr
{
    local expr: -> Expr = parseMultiplicativeExpression();

    if (!expr) {
	return nullptr;
    }

    while (token.kind == PLUS || token.kind == MINUS) {
	local op: TokenKind = token.kind;
        getToken();

	local expr2: -> Expr = parseMultiplicativeExpression();
	if (!expr2) {
	    errorMsg(token.loc, "expression expected\n");
	    errorFatal();
            return nullptr;
        }
	expr = op == PLUS
	    ? createBinaryExpr(EXPR_ADD, expr, expr2)
	    : createBinaryExpr(EXPR_SUB, expr, expr2);
    }
    return expr;
}

fn parseMultiplicativeExpression() : -> Expr
{
    local expr: -> Expr = parsePrimaryExpression();

    if (!expr) {
	return nullptr;
    }

    while (token.kind == ASTERISK || token.kind == SLASH) {
	local op: TokenKind = token.kind;
        getToken();

	local expr2: -> Expr = parsePrimaryExpression();
	if (!expr2) {
	    errorMsg(token.loc, "expression expected\n");
	    errorFatal();
            return nullptr;
        }
	//
	//    FIXME: Gives error 'not an lvalue'
	//    op == ASTERISK
	//	? expr = createBinaryExpr(EXPR_MUL, expr, expr2)
	//	: expr = createBinaryExpr(EXPR_DIV, expr, expr2);
	//
	expr = op == ASTERISK
	    ? createBinaryExpr(EXPR_MUL, expr, expr2)
	    : createBinaryExpr(EXPR_DIV, expr, expr2);
    }
    return expr;
}

fn parsePrimaryExpression() : -> Expr
{
    if (token.kind == IDENTIFIER) {
	local expr: -> Expr =  createIdentifierExpr(token.val);
	getToken();
	return expr;
    } else if (token.kind == DECIMAL_LITERAL) {
	local val: i64 = strtol(token.val, token.val + token.valLen, 10);
	getToken();
	return createLiteralExpr(val);
    } else if (token.kind == HEXADECIMAL_LITERAL) {
	local val: i64 = strtol(token.val, token.val + token.valLen, 16);
	getToken();
	return createLiteralExpr(val);
    } else if (token.kind == OCTAL_LITERAL) {
	local val: i64 = strtol(token.val, token.val + token.valLen, 8);
	getToken();
	return createLiteralExpr(val);
    } else if (token.kind == LPAREN) {
	getToken();
	local expr: -> Expr = parseAdditiveExpression();
	if (!expr) {
	    errorMsg(token.loc, "expression expected\n");
	    errorFatal();
	    return nullptr;
	}
	errorExpected(RPAREN);
	getToken();
	return expr;
    }
    return nullptr;
}
